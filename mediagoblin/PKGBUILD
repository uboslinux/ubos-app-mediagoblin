developer=http://www.mediagoblin.org/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=v0.9.0
pkgrel=2
pkgdesc="Free software media publishing platform"
arch=('any')
license=("AGPL")
source=("http://git.savannah.gnu.org/cgit/mediagoblin.git/snapshot/mediagoblin-${pkgver}.tar.gz")
releasepage=("http://git.savannah.gnu.org/cgit/mediagoblin.git/")

# Do not need to specify dependency on postgresql; ubos-manifest role does this implicitly
depends=('python' 'python-lxml' 'python-pillow' 'python-psycopg2' 'python-setuptools')
makedepends=('git' 'python-virtualenv')
options=('!strip')
sha512sums=('SKIP')
# The checksum cannot currently be checked, as the gzip file downloaded from ${source} keeps having
# changed time stamps in byte 5-8 of the file (GZIP timestamp header)

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    git submodule init && git submodule update

    [ -d site-packages ] || mkdir site-packages
    PYTHONPATH=$(pwd)/site-packages python2 setup.py develop --verbose --install-dir $(pwd)/site-packages

# These are from the mediagoblin instructions, but I can't get them to not include the
# build paths
#
#    virtualenv2 --system-site-packages --no-setuptools .
#    ./bin/python2 ./setup.py develop
#    virtualenv2 --system-site-packages --relocatable .
}

package() {
# Manifest
    mkdir -p ${pkgdir}/var/lib/ubos/manifests
    install -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p ${pkgdir}/srv/http/_appicons/$pkgname
    install -m644 ${startdir}/appicons/{72x72,144x144}.png ${pkgdir}/srv/http/_appicons/$pkgname/

# Templates
    mkdir -p ${pkgdir}/usr/share/${pkgname}/tmpl
    install -m755 ${startdir}/tmpl/{wsgi.py,paste.ini,mediagoblin.ini,htaccess}.tmpl ${pkgdir}/usr/share/${pkgname}/tmpl/

# Data
    mkdir -p ${pkgdir}/var/lib/${pkgname}

# Code
    mkdir -p ${pkgdir}/usr/share/${pkgname}/bin
    install -m755 ${startdir}/bin/init.pl ${pkgdir}/usr/share/${pkgname}/bin/

    mkdir -p ${pkgdir}/usr/share/${pkgname}/mediagoblin

    cp -dr --no-preserve=ownership ${startdir}/src/${pkgname}-${pkgver}/{extlib,m4,mediagoblin,mediagoblin.egg-info,site-packages} ${pkgdir}/usr/share/${pkgname}/mediagoblin

# Celery service
    mkdir -p ${pkgdir}/usr/lib/systemd/system
    install -m755 ${startdir}/systemd/mediagoblin-celeryd\@.service ${pkgdir}/usr/lib/systemd/system/
}
