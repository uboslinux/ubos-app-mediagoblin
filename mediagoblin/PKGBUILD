developer=http://www.mediagoblin.org/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=v0.8.0
pkgrel=1
pkgdesc="Free software media publishing platform"
arch=('any')
license=("AGPL")
source=("http://git.savannah.gnu.org/cgit/mediagoblin.git/snapshot/mediagoblin-${pkgver}.tar.gz")

# Do not need to specify dependency on postgresql; ubos-manifest role does this implicitly
depends=('python2' 'python2-lxml' 'python2-pillow' 'python2-psycopg2' 'python2-setuptools')
makedepends=('git' 'python2-virtualenv')
options=('!strip')
sha512sums=('66a09c63a4f36e49ffc357c3d52d2db956f393d637c276f57597bcd5488301c9667b53dc31d2bd220237f18ab7b729699f77775a2dd8e7d451ddbf14726b4ceb')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    git submodule init && git submodule update

    [ -d site-packages ] || mkdir site-packages
    PYTHONPATH=$(pwd)/site-packages python2 setup.py develop --verbose --install-dir $(pwd)/site-packages

# These are from the mediagoblin instructions, but I can't get them to not include the
# build paths
#
#    virtualenv2 --system-site-packages --no-setuptools .
#    ./bin/python2 ./setup.py develop
#    virtualenv2 --system-site-packages --relocatable .
}

package() {
# Manifest
    mkdir -p ${pkgdir}/var/lib/ubos/manifests
    install -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p ${pkgdir}/srv/http/_appicons/$pkgname
    install -m644 ${startdir}/appicons/{72x72,144x144}.png ${pkgdir}/srv/http/_appicons/$pkgname/

# Templates
    mkdir -p ${pkgdir}/usr/share/${pkgname}/tmpl
    install -m755 ${startdir}/tmpl/{wsgi.py,paste.ini,mediagoblin.ini,htaccess}.tmpl ${pkgdir}/usr/share/${pkgname}/tmpl/

# Data
    mkdir -p ${pkgdir}/var/lib/${pkgname}

# Code
    mkdir -p ${pkgdir}/usr/share/${pkgname}/bin
    install -m755 ${startdir}/bin/init.pl ${pkgdir}/usr/share/${pkgname}/bin/

    mkdir -p ${pkgdir}/usr/share/${pkgname}/mediagoblin

    cp -dr --no-preserve=ownership ${startdir}/src/${pkgname}-${pkgver}/{extlib,m4,mediagoblin,mediagoblin.egg-info,site-packages} ${pkgdir}/usr/share/${pkgname}/mediagoblin

# Celery service
    mkdir -p ${pkgdir}/usr/lib/systemd/system
    install -m755 ${startdir}/systemd/mediagoblin-celeryd\@.service ${pkgdir}/usr/lib/systemd/system/
}
